











module GTmodeProject::WheelShopRoot
{
    static current_wheel_category;
    static current_wheel_maker;
    static current_car;
    static current_list;
    static last_tried_wheel;
    static is_maker_wheels;
    static price;

    function getUsedWheelPeriod()
    {
        var num = 0;
        var flg = true;

        while (WheelData.defined("used%02d_carlist".format(num)))
            num++;

        return num;
    }

    static period;

    function onLoad(context)
    {
        period = getUsedWheelPeriod();
    }

    function getWheelList()
    {
        // The wheel list and is_maker_wheels are passed in from WheelMakerRoot if loading a maker's wheels - if this is the case just return it early
        if (is_maker_wheels)
            return current_list;

        var listname;
        if (current_wheel_maker == "used")
            listname = "used%02d_carlist".format(main::game.calendar.getElapsedDate(period));
        else
            listname = current_wheel_maker + "_carlist";


        if (!WheelData.defined(listname))
        {
            MakerWheelData.load("/projects/specii/makerwheelinfo");
            if (!MakerWheelData::MakerWheels.defined(listname))
                return [];
            else 
            {
                is_maker_wheels = true;
                return MakerWheelData::MakerWheels[listname];
            }
        }
        else
            return WheelData[listname];
    }

    function onInitialize(context)
    {
        hidden.visible = false;

        enterPage(context, ROOT, "WheelShopTitle");
        ROOT["Common"]["ToolTip"]["tip"].remove();
        ROOT["Common"]["ToolTip"]["tip"].value = context.translate(WingShopRoot, "how to try this on");

        current_car = main::game.garage.riding_car;
        current_car.setupPartsBuffer();
        current_list = getWheelList();

        if (!is_maker_wheels)
            price = 500;
        else
            price = 1000;

        Price.text = main::menu::MUtility::GetMoneyString(price);

        if (current_wheel_maker == "used")
        {
            UsedLabel.visible = true;
            Logo.visible = false;
        }
        else
        {
            UsedLabel.visible = false;
            Logo.visible = true;
            Logo.image_path = "image/gtmode/gtauto/logo/" + current_wheel_maker + ".png";
            Logo.doInitialize(context);
        }

        if (main::game.garage.hasRidingCar())
        {
            CarFace.car_garage = current_car;
            CarFace.action_type = "normal";
            CarFace.quality_level = 1;
            CarFace.syncLoading();
            CarFace.car_model.motion = 2;
            CarFace.loadModelOptionMotion("image/gtmode/gtauto/wheelShop_camera.mot");
        }
        
        var length = current_car.getLength().toFloat() / 1000.0;
        CarFace.car_model.tx = 0.0;
        CarFace.car_model.tz = (length * 0.5) - 1.5;
        CarFace.car_model.ry = 0.0;
        CarFace.car_model.warp();

        var size = current_list.size;
        for (var i = 0; i < size; i++)
        {
            var wheel = current_list[i];
            var item = hidden::Item.doCopy();
            item["bg"].image_path = "image/piece/wheel_mini/" + wheel + ".png";

            if (!context.existGpbBinary("image/piece/wheel_mini/" + wheel + ".png") || is_maker_wheels)
                item["bg"].image_path = "image/piece/wheel_mini/generic.png";

            ScrollBox::SelectBox.appendChild(context, item);
            item.defineStatic("wheel", wheel);
        }

        ScrollBox::SelectBox.callFocus(ROOT);
    }

    function onFinalize(context)
    {
        ScrollBox::SelectBox.clearChildren(context);
        
        if (ROOT.defined("MakerWheelData"))
            MakerWheelData.clearStatic();

        leavePage(context, ROOT);

        current_wheel_maker = nil;
        current_list = nil;
        last_tried_wheel = nil;
    }

    function onCancel(context)
    {
        main::sound.play("cancel");
        start_page(context, WheelMakerRoot);
        return EVENTRESULT_FILTER;
    }

    function tryon(wheel)
    {
        if (last_tried_wheel != nil && last_tried_wheel == wheel)
            return;

        CarFace.wheel = wheel;
        last_tried_wheel = wheel;
    }

    function onKeyPress(context, event)
    {
        switch (event.keysym)
        {
            case PS2_PAD_CTRL_L1:
                if (ScrollBox::SelectBox.index > 0)
                    main::sound.play("cursor");

                ScrollBox::SelectBox.index -= 10;
                return EVENTRESULT_FILTER;

            case PS2_PAD_CTRL_R1:
                if (ScrollBox::SelectBox.index < (ScrollBox::SelectBox.Count() - 1))
                    main::sound.play("cursor");

                ScrollBox::SelectBox.index += 10;
                return EVENTRESULT_FILTER;

            case PS2_PAD_CTRL_SELECT:
                main::sound.play("ok");
                QuickMenuPopup::open(context);
                return EVENTRESULT_FILTER;
        }
        return EVENTRESULT_CONTINUE;
    }

    function onActivate(context, event)
    {
        var wheel = event.widget["wheel"];
        logInfo("Wheel = %{wheel}, category = %{current_wheel_category}, haveTireCategoryWheel = %{current_car.haveTireCategoryWheel(current_wheel_category, wheel)}");
        tryon(wheel);

        if (current_car.haveTireCategoryWheel(current_wheel_category, wheel))
        {
            main::sound.play("disable");
            openConfirmDialog(context, 2, context.translate(ROOT, "You have this wheel"));
        }
        else
        {
            if (!sureEnoughMoney(context, price))
                return EVENTRESULT_FILTER;

            main::sound.play("ok");

            var res = openConfirmDialog(context, 1, context.translate(ROOT, "Do you buy this?"));
            if (res)
            {
                main::game.incMoney(-price);
                refreshMoney(ROOT);
                main::game.calendar.putBuyWheelEvent(wheel, current_wheel_category, current_wheel_maker == "used" ? true : false);
                current_car.setTireCategoryWheel(current_wheel_category, wheel);
                current_car.syncWheel();
                refreshMoney(ROOT);

                openConfirmDialog(context, 0, context.translate(ROOT, "You bought this"));
                start_page(context, GTAutoRoot);
            }
        }

        return EVENTRESULT_FILTER;
    }
}

module GTmodeProject::WheelShopRoot::ScrollBox::SelectBox
{
    function onValueChanged(context, index) { }
}

module GTmodeProject::WheelShopRoot::hidden::Item
{
    function onFocusEnter(context, event)
    {
        var item = event.widget;
        var name = item["wheel"];
        Name.text = getWheelName(context, name, current_wheel_maker == "used" || is_maker_wheels);
        tryon(name);
        print name;
        return EVENTRESULT_FILTER;
    }
}