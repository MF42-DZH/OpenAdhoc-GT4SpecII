







#define FLAG_BONNET_CAM (1);
#define FLAG_INTERIOR_CAM (1 << 1);
#define FLAG_GT3_CAM (1 << 2);
#define FLAG_ROOF_CAM (1 << 3);

module BootProject
{
    static DEMO = main.defined("DEMO_SKIP_CHECKING_MEMCARD");

    #include "../share/dialog.ad";
    #include "../share/page_util.ad";
    #include "../share/memorycard.ad";
    #include "../share/course_define.ad";

    // Loads "<region>_carlist.adc"
    //BootProject.load(main::game.option.game_zone + "_carlist");

    function logInfo(msg)
    {
        print "\n--------------------\n";
        print "\n%{msg}\n";
        print "\n--------------------\n";
    }

    function initCourse()
    {
        var list = CourseData::all_crslist;
        for (var i = 0; i < list.size; i++)
        {
            var course = list[i];
            main::game.course_record.create(course.code);

            if (course.has_reverse)
                main::game.course_record.create("r_" + course.code);
        }
    }

    function initAvailable()
    {
        var list = InitData::car_carlist;
        for (var i = 0; i < list.size; i++)
        {
            print i;
            if (list[i] != nil)
                main::game.setCarAvailable(list[i]);
        }

        list = InitData::course_carlist;
        for (var i = 0; i < list.size; i++)
        {
            var course = list[i];
            if (CourseData.defined(course))
            {
                main::game.setCourseAvailable(course);
                if (CourseData[course].has_reverse)
                    main::game.setCourseAvailable("r_" + course);
            }
        }
    }

    function initFavorite()
    {
        if (main::game.favorite_car.getSize() <= 0)
        {
            main::game.favorite_car.clear();
            var list = InitData::favorite_car_carlist;

            for (var i = 0; i < list.size; i++)
            {
                main::game.favorite_car.append(list[i]);
            }
        }

        if (main::game.favorite_course.getSize() <= 0)
        {
            main::game.favorite_course.clear();
            var list = InitData::favorite_course_carlist;

            for (var i = 0; i < list.size; i++)
            {
                main::game.favorite_course.append(list[i]);
            }
        }
    }

    function fillCarAndMoney()
    {
        var list = nil;
        if (main::menu::MSystem::GetProductCode().beginWith("SCES"))
        {
            if (!InitData.defined("car_carlist"))
                return;
            
            list = InitData::car_carlist;
        }
        else
        {

            if (!InitData.defined("garage_carlist"))
                return;

            list = InitData::garage_carlist;
        }

        var garage = main::game.garage;
        for (var i = 0; i < list.size; i++)
        {
            var car = list[i];
            var col = 0;
            garage.addNewCar(car, col);
            main::game.setCarAvailable(car);
        }

        main::game.incMoney(10000000);
    }

    function onLoad(context)
    {
        context.createRenderContext(1);
        var render_context = context.getRenderContext(0);

        main::menu::MMovieFace::initialize(render_context);
        render_context.transition = main::menu::MColorTransition();
        render_context.transition.panIn(render_context);

        LanguageRoot::onLoad(render_context);

        // No real need to do these anymore as everything is always available in arcade + favourites will be set up by the player
        /*
        if (main::game.last_sequence == "boot")
        {
            initCourse();
            initAvailable();
            initFavorite();
        }
        */

        if (main::game.next_menu_arg == "")
            render_context.startPage(LoadRoot)
        else
            render_context.startPage(BootProject[main::game.next_menu_arg]);
    }

    function onUnload(context)
    {
        main::menu::MMovieFace::terminate();
    }

    function exitBootSequence(context)
    {
        if (main::game.major_menu_project == "")
        {
            start_page(context, MovieRoot);
        }
        else
        {
            main::game.next_menu_project = main::game.major_menu_project;
            main::game.next_menu_arg == ""; // That's in original code, Is this intentional or is it supposed to be '='?
            start_page(context, nil);
        }
    }

    function exitLoaded(context)
    {
        clearAutoSaveDisabled();

        main::menu::MMemoryCardManager::setExistValidGameData(true);

        if (main::game.option.opening_after_autoload)
        {
            start_page(context, MovieRoot);
        }
        else
        {
            endBoot(context);
        }
    }

    function endBoot(context)
    {
        var ModeString = ["normal", "480p", "1080i"];
        var settings = main::game.option.entrance_addr.split(";");
        for (var i = 0; i < settings.size; i++)
        {
            var [k, v] = settings[i].split("=");
            print [k, v];
            switch (k)
            {
                case "vm":
                    // If for some reason the saved video mode was invalid, e.g. skipping boot setup but making it past the intro, set it to 480p
                    // This is fine for non-480p screens on real hardware as menus are always interlaced anyway
                    if (v < 0)
                        v = 1;
                    main::game.option.race_display_mode = ModeString[v];
                    break;

                case "c":
                    var val = v.toInt();
                    //print "Saved camera flags == %{val}";
                    var bonnetCamActive = val & FLAG_BONNET_CAM;
                    var interiorCamActive = val & FLAG_INTERIOR_CAM;
                    var gt3CamActive = val & FLAG_GT3_CAM;
                    var roofCamActive = val & FLAG_ROOF_CAM;

                    // If you're using this code to build your own mod you probably want to remove these calls
                    // New engine functions added by my C code - if you're using a vanilla executable these won't exist so these calls will crash
                    bonnetCamActive ? main::game.toggleBonnetCam(1) : main::game.toggleBonnetCam();
                    interiorCamActive ? main::game.toggleInteriorCam(1) : main::game.toggleInteriorCam();
                    gt3CamActive ? main::game.toggleGT3Cam(1) : main::game.toggleGT3Cam();
                    roofCamActive ? main::game.toggleRoofCam(1) : main::game.toggleRoofCam();
                    break;

                case "g":
                    print "Setting gauge mode to %{v}";
                    main::game.toggleGaugeMode(v.toInt());
                    break;

                case "tm":
                    print "Setting transmission volume to %{v}";
                    main::game.setTransmissionVolume(v.toInt());
                    break;
            }
        }

        print "made it here";

        if (main::game.major_menu_project == "gtmode")
        {
            start_project(context, "gtmode");
        }
        else
        {
            start_project(context, "arcade");
        }
    }
}